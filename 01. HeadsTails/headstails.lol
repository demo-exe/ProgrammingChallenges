HAI 1.2
CAN HAS STDIO?
I HAS A SCOR ITZ 0
I HAS A GES ITZ "TEEL"
I HAS A SEED

HOW IZ I RANDOMMIN BTW IZ ONLY ZEROS AN ONES
    SEED R MOD OF SUM OF 12345 AN PRODUKT OF 1103515245 AN SEED AN 4294967296 
    I HAS A RET ITZ QUOSHUNT OF SEED AN 1024 BTW NOT LAST BIT PLOX
    RET IS NOW A NUMBR
    FOUND YR MOD OF RET AN 2
IF U SAY SO

HOW IZ I RND_GES
    BOTH SAEM I IZ RANDOMMIN MKAY AN 0
    O RLY?
        YA RLY
            FOUND YR "TEEL"
        NO WAI
            FOUND YR "HED"
    OIC
IF U SAY SO

HOW IZ I GETTIN 
    IM IN YR LUP UPPIN YR CNTER
        VISIBLE "GIMMEH UR GES PLX :"
        GIMMEH GES
        EITHER OF BOTH SAEM GES AN "TEEL" AN BOTH SAEM GES AN "HED"
        O RLY?
            YA RLY
                FOUND YR GES
            NO WAI
                VISIBLE "UR GES MUSS BE TEEL OR HED, RLY XD"
        OIC
    IM OUTTA YR LUP
IF U SAY SO

HOW IZ I PLAYIN 
    IM IN YR LUP UPPIN YR CNTER
        VISIBLE SMOOSH "--[ UR SCOR IS " AN SCOR AN " ]--" MKAY
        
        I HAS A MYGES ITZ I IZ RND_GES MKAY
        GES R I IZ GETTIN MKAY
        BOTH SAEM GES AN MYGES, O RLY?
            YA RLY
                VISIBLE "GJ XD"
                SCOR R SUM OF SCOR AN 1
            NO WAI
                VISIBLE "NAH XD"
        OIC
	VISIBLE ""
    IM OUTTA YR LUP
IF U SAY SO

VISIBLE "U WNT PLAY A GAEM [Y/N]??"
I HAS A PLAI
GIMMEH PLAI
DIFFRINT PLAI AN "Y", O RLY?
    YA RLY
        VISIBLE "k :'("
    NO WAI
        VISIBLE "GIMMEH A SEED PLX"
        GIMMEH SEED
        
        I IZ PLAYIN MKAY
OIC

KTHXBYE
